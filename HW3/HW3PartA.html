
<!DOCTYPE html>
<html lang="en">
	<head>
		<title>HW3 Part A</title>
		<meta charset="utf-8">
		<style>
			body {
			  	margin: 0px;
				overflow: hidden;
			}
		</style>
	</head>
<body>

<div id="container"></div>

  <script src="../js/three.js"></script>
  

  <script id="vertexShader" type="x-shader/x-vertex">
	uniform mat4 modelViewMatrix;
  uniform mat4 projectionMatrix;

	attribute vec3 position;
	
  void main() {
 		gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
	}
  </script>

  <script id="fragmentShader" type="x-shader/x-fragment">
  precision mediump float;
  
	const int MAX_MARCHING_STEPS = 255;
  const float MIN_DIST = 0.0;
  const float MAX_DIST = 100.0;
  const float EPSILON = 0.0001;
  const mat3 rot = mat3(0.7220079,  0.2779921,  0.6335810,
                        0.2779921,  0.7220079, -0.6335810,
                       -0.6335810,  0.6335810,  0.4440158);
  
  uniform float rx;
  uniform float ry;
  uniform float time;
  
  float maxcomp(vec3 p ) {
    return max(p.x,max(p.y,p.z));
  }
  float maxcomp(vec2 p) {
    return max(p.x, p.y);
  }
  
  float sdf_blend(float d1, float d2, float a)
  {
    return a * d1 + (1.0 - a) * d2;
  }
  
  float sphereSDF(vec3 samplePoint) {
      return length(samplePoint) - 1.0;
  }
  
  float boxSDF( vec3 p, vec3 b )
  {
    vec3  di = abs(p) - b;
    float mc = maxcomp(di);
    return min(mc,length(max(di,0.0)));
  }

  float crossSDF(vec3 p)
  {
    float da = maxcomp(abs(p.xy));
    float db = maxcomp(abs(p.yz));
    float dc = maxcomp(abs(p.zx));
    return min(da,min(db,dc))-1.0;
  }
  
  vec4 sceneSDF(vec3 p)
  {
    float ang = time/1000.0;
    mat3 rot_mat = mat3(1.0,  0.0,       0.0,
                        0.0,  cos(ang),  sin(ang), 
                        0.0, -sin(ang),  cos(ang));
    mat3 rot_mat2 = mat3(cos(ang),  sin(ang),   0.0,
                        -sin(ang),  cos(ang),   0.0, 
                         0.0,       0.0,        1.0 );
    float dist = boxSDF(rot_mat2 * rot_mat * p,vec3(1.0));
    //float dist = boxSDF(p,vec3(1.0));
    vec4 res = vec4( dist, 1.0, 0.0, 1.0);

    float scale = 1.0;
    const int iters = 4;
    for( int m=0; m<iters; m++ )
    {
      vec3 a = mod( rot_mat2 * rot_mat * p * scale, 2.0 )-1.0;
      scale *= 3.0;
      vec3 r = rot_mat2 * rot_mat * abs(1.0 - 3.0*abs(a));

      float da = max(r.x,r.y);
      float db = max(r.y,r.z);
      float dc = max(r.z,r.x);
      float c = (min(da,min(db,dc))-1.0)/scale;

      if( c > dist )
      {
        dist = c;
        res = vec4( dist, float(m)/float(iters), float(m)/float(iters), float(m)/float(iters));
      }
    }
   
    //vec4 box2 = vec4(sphereSDF(rot_mat2 * rot_mat * p, vec3(1.0)), 0.0, 0.0, 1.0);
    //vec4 box2 = vec4(sphereSDF(rot_mat2 * rot_mat * p), 0.0, 0.0, 1.0);
    //return box2;
    return res;
    //return vec4(mix(box2, res, sin(time/1000.0)));
  }
  
  /**
   * Signed distance function for a sphere centered at the origin with radius 1.0;
   */
  
  /*float sceneSDF(vec3 z) {
    z = mod(z,1.0)-vec3(0.5); // instance on xy-plane
    return length(z)-0.3;             // sphere DE
  }*/
  vec4 shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end) {
    float depth = start;
    vec4 result = vec4(0.0, 0.0, 0.0, 0.0);
    for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
      result = sceneSDF(eye + depth * marchingDirection);
      float dist = float(result.x);
      if (dist < EPSILON) {
        return vec4(depth, result.yzw);
      }
      depth += dist;
      if (depth >= end) {
        return vec4(end, result.yzw);
      }
    }
    return vec4(end, result.yzw);
  }

  vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
    vec2 xy = (fragCoord - size / 2.0);
    float z = size.y / tan(radians(fieldOfView) / 2.0);
    return normalize(vec3(xy, -z));
  }
  
  vec3 estimateNormal(vec3 p) {
    return normalize(vec3(
      sceneSDF(vec3(p.x + EPSILON, p.y, p.z)).x - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)).x,
      sceneSDF(vec3(p.x, p.y + EPSILON, p.z)).x - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)).z,
      sceneSDF(vec3(p.x, p.y, p.z  + EPSILON)).x - sceneSDF(vec3(p.x, p.y, p.z - EPSILON)).x
    ));
  }
  
  vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye,
                          vec3 lightPos, vec3 lightIntensity) {
    vec3 N = estimateNormal(p);
    vec3 L = normalize(lightPos - p);
    vec3 V = normalize(eye - p);
    vec3 R = normalize(reflect(-L, N));
    
    float dotLN = dot(L, N);
    float dotRV = dot(R, V);
    
    if (dotLN < 0.0) {
      // Light not visible from this point on the surface
      return vec3(0.0, 0.0, 0.0);
    } 
    
    if (dotRV < 0.0) {
      // Light reflection in opposite direction as viewer, apply only diffuse
      // component
      return lightIntensity * (k_d * dotLN);
    }
    return lightIntensity * (k_d * dotLN + k_s * pow(dotRV, alpha));
  }
  
  vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye) {
    const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
    vec3 color = ambientLight * k_a;
    
    vec3 light1Pos = vec3(4.0, 2.0, 4.0);
    vec3 light1Intensity = vec3(0.4, 0.4, 0.4);
    
    color += phongContribForLight(k_d, k_s, alpha, p, eye, light1Pos, light1Intensity);
    
    vec3 light2Pos = vec3(2.0, 2.0, 2.0);
    vec3 light2Intensity = vec3(0.4, 0.4, 0.4);
    
    color += phongContribForLight(k_d, k_s, alpha, p, eye, light2Pos, light2Intensity);    
    return color;
  }

  void main()
  {
    vec3 dir = rayDirection(45.0, vec2(rx, ry), gl_FragCoord.xy);
    vec3 eye = vec3(0.0, 0.0, 5.0);
    vec4 result = shortestDistanceToSurface(eye, dir, MIN_DIST, MAX_DIST);
    float dist = result.x;
    
    if (dist > MAX_DIST - EPSILON) {
        // Didn't hit anything
        gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
		return;
    }
    
    // The closest point on the surface to the eyepoint along the view ray
    vec3 p = eye + dist * dir;
    
    vec3 K_a = vec3(0.2, 0.2, 0.2);
    vec3 K_d = vec3(0.7, 0.2, 0.2);
    vec3 K_s = vec3(1.0, 1.0, 1.0);
    float shininess = 10.0;
    
    vec3 color = phongIllumination(K_a, K_d, K_s, shininess, p, eye);
    
    //gl_FragColor = vec4(color, 1.0);
    gl_FragColor = vec4(mix(result.yzw, color, 0.5), 1.0);
  }
	</script>



	<script>
		
var scene;
var camera;
var renderer;
var resX = 300;
var resY = 300;
var mesh;
var material;
var FBO_A, FBO_B;
var plane;
var fullScreenQuad;
var vs = document.getElementById( 'vertexShader' ).textContent;
var fs = document.getElementById( 'fragmentShader' ).textContent;

scene_setup(); //initialize the Three.js scene

function scene_setup(){
	//This is the basic scene setup
	scene = new THREE.Scene();
	var width = window.innerWidth;
	var height = window.innerHeight;
  
  var uniforms = {
			rx: {type: "f", value: width},
			ry: {type: "f", value: height},
      time: {type: "f", value: 0.0},
		};
  material = new THREE.RawShaderMaterial( {
    uniforms: uniforms,
    vertexShader: vs,
    fragmentShader: fs,	
  } );
    
  //we can use a Three.js Plane Geometry along with the orthographic camera to create a "full screen quad"
	plane = new THREE.PlaneBufferGeometry( window.innerWidth, window.innerHeight )
	//Draw textureB to screen 
	fullScreenQuad = new THREE.Mesh( plane, material );
	scene.add(fullScreenQuad);

	//orthographic camera can be used for 2D
	camera = new THREE.OrthographicCamera( width / -2, width / 2, height / 2, height / -2, 0.1, 1000 );
	camera.position.z = 0.2;

	renderer = new THREE.WebGLRenderer();
	renderer.setSize( window.innerWidth, window.innerHeight );
	document.body.appendChild( renderer.domElement );
}

render();

function render() {
	requestAnimationFrame( render );
  var time = performance.now();
  fullScreenQuad.material.uniforms.time.value = time;
	//Then draw the full sceen quad to the on screen buffer, ie, the display
	renderer.render( scene, camera );
}
	</script>

</body>
</html>

